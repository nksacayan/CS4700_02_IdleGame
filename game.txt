Let's make a game!
	name:Adventurer's Guild Clicker
	by:Nicholas Sacayan
	desc:An idle game for Professor Summerville's CS 4700.02

Settings
	// CSS reset from pastebin
	stylesheet:https://pastebin.com/raw/JxMuvhXd
	no bulk particles

CSS
	.box {
		border: thin solid white;
		overflow: hidden;
		position: static;
	}

	#box-LogBox {
		max-height:150px;
	}

	#log {
    height: 100px;
	}

Layout
	*ResourceBox
		contains:Resources
		header:Resources

	*ButtonBox
		contains:Buttons
		header:Buttons

	*BuildingBox
		contains:Buildings
		header:Buildings

	*UpgradeBox
		contains:Upgrades
		header:Upgrades

	*AchievementBox
		contains:Achievements
		header:Achievements

	*CombatBox
		header:Combat Box
		contains:tag:combatStats, tag:fight, tag:combatActions, tag:spells, tag:items, tag:enemyStats
		ps:hide

	*LogBox
		contains:Log
		header:Log

	Buttons
		// Training options
			*TEMPLATE
				on click:if (inCombat) log Can't increase stats while in combat!

			  *trainButton
			    	name:Train
					desc:Increases Strength
			    	on click:if (no inCombat) yield 1 str

				*drinkButton
					name:Drink
					desc:Increases Constitution
					on click:
						if (no inCombat)
							yield 1 con
							yield 2 maxHealth
						end
					end

				*studyButton
			    	name:Study
					desc:Increases Intelligence
			    	on click:if (no inCombat) yield 1 int

				*meditateButton
					name:Meditate
					desc:Increases Focus
					on click:
						if (no inCombat)
							yield 1 focus
							yield 2 maxMana
						end
					end

		// Fights
			*TEMPLATE
				on click:
					if (have inCombat)
						log You're already in combat!
					else
						log You've found an enemy!
						show tag:enemyStats
						show tag:combatActions
						hide tag:fight
					end
				end
				tag:fight

				*fight1
					name:First Monster
					on click:
						if (no inCombat)
							yield 100 enemyHealth
							yield 10 enemyAttack
							yield 10 enemyGold
							enemyAchievement=1
						end
						[include startCombat]
					end

				*fight2
					name:Second Monster
					hidden
					on click:
						if (no inCombat)
							yield 1000 enemyHealth
							yield 100 enemyAttack
							yield 100 enemyGold
							enemyAchievement=2
						end
						[include startCombat]
					end

				*fight3
					name:Final Monster
					hidden
					on click:
						if (no inCombat)
							yield 10000 enemyHealth
							yield 1000 enemyAttack
							yield 1000 enemyGold
							enemyAchievement=3
						end
						[include startCombat]
					end

		// Combat
			*TEMPLATE
				hidden
				tag:combatActions

				*attackButton
					name:Attack
					on click:
						if (have inCombat)
							// do damage
							[include applyAttack]
							// take damage
							[include enemyTurn]
							// check battle status
							[include checkBattleStatus]
						end
					end

				*fleeButton
					name:Flee
					desc:Flee the battle!
					on click:
						[include endCombat]
						log You flee combat!
					end

				*spellButton
					name:Spells
					on click:
						show tag:spells
						hide tag:combatActions
					end

		// Spellcasting
			*TEMPLATE
			hidden
			tag:spells

				*fireball
					name:Fireball
					desc:Lobs a fireball at your enemies for 2*intelligence ([2*int]) damage!
					on click:
						if (have inCombat)
							lose (2*int) enemyHealth
							lose (int/4) mana
							log You cast Fireball for [2*int] damage!
							[include enemyTurn]
							[include checkBattleStatus]
						end
					end

				*heal
					name:Heal
					desc:Heals self by *placeholder*
					on click:
						if (have inCombat)
							yield (2*int) health
							lose (int/4) mana
							log You cast Heal and regain [2*int] health!
							[include enemyTurn]
							[include checkBattleStatus]
						end
					end

				*spellButtonReturn
					name:Back
					desc:Hide spells and return to combat actions.
					on click:
						show tag:combatActions
						hide tag:spells
					end

		// Other
		*TEMPLATE

			*clearLogButton
				name:Clear Log
				desc:Clears the text log
				on click:clear log

			*debugButton
				name:Debug Button
				desc:Does whatever I need to debug. Remove before actual game.
				on click:
					log inCombat=[inCombat]
				end

	Resources
		// Makeshift global variables
		*TEMPLATE
			hidden

			*enemyAchievement

		// Player combat stats
		*TEMPLATE
			start with:100
			tag:combatStats

			*health
		    name:Health
				on tick:if (no inCombat and this<maxHealth) yield 1 this
				desc:Decreases when you take damage. You flee when this hits 0.

			*maxHealth
				name:Max Health

			*mana
				name:Mana
				on tick:if (no inCombat and this<maxMana) yield 1 this
				desc:This resource is used to cast spells.

			*maxMana
				name:Max Mana

		*TEMPLATE
			start with:1

			*str
				name:Strength
				desc:Increases attack damage

			*con
				name:Constitution
				desc:Increases health
				// Fake stat because engine can't tick properly

			*int
				name:Intelligence
				desc:Increases the power of magic

			*focus
				name:Focus
				desc:Increases mana
				// Fake stat

		*TEMPLATE

			*gold
				name:Gold
				desc:Use this to buy things

		// Enemy Combat Stats
			*inCombat
				hidden

			*TEMPLATE
				tag:enemyStats
				hidden

				*enemyHealth
					name:Enemy Health

				*enemyAttack
					name:Enemy Attack

				*enemyGold
					name:Gold Reward
					desc:How much gold you get for killing this enemy.

Buildings

	// Gold Builders
		*lemonadeStand
			name:Lemonade Stand
			desc:They don't have grapes.</>+2 gold/s
			req:goldMilestone1
			cost:10 gold
			on tick:yield 2 gold

		*oilDrill
			name:Oil Drill
			desc:Hire the mole people to dig up oil.</>+20 gold/s
			req:goldMilestone2
			cost:100 gold
			on tick:yield 20 gold

		*gmeStonks
			name:GME Stonks
			desc:Grow diamond hands.</>+200 gold/s
			req:goldMilestone3
			cost:1000 gold
			on tick:yield 200 gold

	// Trainers
		// Strength
			*strTrainer1
				req:have strMilestone1
				name:Personal Trainer
				desc:Helps you get big.</>+1 Strength/s
				cost:10 gold
				on tick:yield 1 str

			*strTrainer2
				req:have strMilestone2
				name:Personal Pro-Wrestler
				desc:Helps you act big.</>+5 Strength/s
				cost:100 gold
				on tick:yield 5 str

			*strTrainer3
				req:have strMilestone3
				name:Personal Jesus
				desc:Someone to hear your prayers, someone who cares.</>+10 Strength/s
				cost:1000 gold
				on tick:yield 10 str

		// Constitution
			*conTrainer1
				req:have constMilestone1
				name:Drinking Buddy
				desc:It takes two to tango.</>+1 Constitution/s
				cost:10 gold
				on tick:
					yield 1 con
					yield 2 maxHealth
				end

			*conTrainer2
				req:have constMilestone2
				name:Frat Bros
				desc:Now it's a party.</>+5 Constitution/s
				cost:100 gold
				on tick:
					yield 5 con
					yield 10 maxHealth
				end

			*conTrainer3
				req:have constMilestone3
				name:The Guzzler
				desc:It's the hat with staws and cupholders.</>+10 Constitution/s
				cost:1000 gold
				on tick:
					yield 10 con
					yield 20 maxHealth
				end

		// Intelligence
			*intTrainer1
				req:have intMilestone1
				name:Library Pass
				desc:Time to hit the books.</>+1 Intelligence/s
				cost:10 gold
				on tick:yield 1 int

			*intTrainer2
				req:have intMilestone2
				name:Magic Tutor
				desc:Gives your zap a little zing.</>+5 Intelligence/s
				cost:100 gold
				on tick:yield 5 int

			*intTrainer3
				req:have intMilestone3
				name:Magical Research Team
				desc:Knowledge is power.</>+10 Intelligence/s
				cost:1000 gold
				on tick:yield 10 int

		// Focus
			*focusTrainer1
				req:have focusMilestone1
				name:Yoga Instructor
				desc:Loosens the muscles and the mind.</>+1 Focus/s
				cost:10 gold
				on tick:
					yield 1 focus
					yield 2 maxMana
				end

			*focusTrainer2
				req:have focusMilestone2
				name:Spell Focus
				desc:An object to concentrate your spells.</>+5 Focus/s
				cost:100 gold
				on tick:
					yield 5 focus
					yield 10 maxMana
				end

			*focusTrainer3
				req:have focusMilestone3
				name:Magic Grass
				desc:Clears the mind.</>+10 Focus/s
				cost:1000 gold
				on tick:
					yield 10 focus
					yield 20 maxMana
				end

Upgrades
	// Active Upgrades
		// Strength
			*trainingUpgrade1
				name:Practice Sword
				desc:A wooden sword used for practice</>+1 Strength per Train
				req:strMilestone1
				cost:10 gold
				passive:increase yield of trainButton by 1

			*trainingUpgrade2
				name:Weighted Training Vest
				desc:A Turtle School Uniform. It's very heavy.</>+5 Strength per Train
				req:strMilestone2
				cost:100 gold
				passive:increase yield of trainButton by 5

			*trainingUpgrade3
				name:Montage Music
				req:strMilestone3
				desc:Without his montage, Rocky is nothing.</>+10 Strength per Train
				cost:1000 gold
				passive:increase yield of trainButton by 10

		// Constitution
			*drinkingUpgrade1
				name:Bigger Mug
				desc:It fits more.</>+1 Constitution per Drink
				cost:10 gold
				req:constMilestone1
				passive:
					increase con yield of drinkButton by 1
					increase maxHealth yield of drinkButton by 2
				end

			*drinkingUpgrade2
				name:Toothpick Umbrellas
				req:constMilestone2
				desc:Its fancy now.</>+5 Constitution per Drink
				cost:100 gold
				passive:
					increase con yield of drinkButton by 5
					increase maxHealth yield of drinkButton by 10
				end

			*drinkingUpgrade3
				name:Second Kidney
				req:constMilestone3
				desc:Nothing can stop you now.</>+10 Constitution per Drink
				cost:1000 gold
				passive:
					increase con yield of drinkButton by 10
					increase maxHealth yield of drinkButton by 20
				end

		// Intelligence
			*studyingUpgrade1
				name:Set of Bookmarks
				desc:Helps keep your place!</>+1 Intelligence per Study
				req:intMilestone1
				cost:10 gold
				passive:increase yield of studyButton by 1

			*studyingUpgrade2
				name:Thinking Cap
				desc:Put on your thinking cap!</>+5 Intelligence per Study
				req:intMilestone2
				cost:100 gold
				passive:increase yield of studyButton by 5

			*studyingUpgrade3
				name:Lofi Hip Hop
				desc:Chill beats to study to.</>+10 Intelligence per Study
				req:intMilestone3
				cost:1000 gold
				passive:increase yield of studyButton by 10

		// Focus
			*meditateUpgrade1
				name:Scented Candles
				desc:They smell nice.</>+1 Focus per Meditate
				cost:10 gold
				req:focusMilestone1
				passive:
					increase focus yield of meditateButton by 1
					increase maxMana yield of meditateButton by 2
				end

			*meditateUpgrade2
				name:Cool Gong
				desc:GOOOOOOOONGGGGGG</>+5 Focus per Meditate
				cost:100 gold
				req:focusMilestone2
				passive:
					increase focus yield of meditateButton by 5
					increase maxMana yield of meditateButton by 10
				end

			*meditateUpgrade3
				name:Zen Garden
				desc:Very peaceful.</>+10 Focus per Meditate
				cost:1000 gold
				req:focusMilestone1
				passive:
					increase focus yield of meditateButton by 10
					increase maxMana yield of meditateButton by 20
				end

	// Passive Upgrades
		// Health
			*regenUpgrade1
				name:Rags
				desc:At least you can wrap your wounds.</>+1 passive Health Regen
				cost:10 gold
				passive:increase yield of health by 1

			*regenUpgrade2
				name:Healer's Kit
				desc:Only once per short rest(just kidding).</>+10 passive Health Regen
				cost:100 gold
				req:fight1Win
				passive:increase yield of health by 10

			*regenUpgrade3
				name:Magical Gummi Vitamins
				desc:They're delicious.</>+1 passive Health Regen
				cost:1000 gold
				req:fight2Win
				passive:increase yield of health by 100

		// Mana
			*manaRegen1
				name:Jeweled Necklace
				desc:I feel my magic coming back, somehow.</>+1 passive Mana Regen
				cost:10 gold
				passive:increase yield of mana by 1

			*manaRegen2
				name:Enchanted Hat
				desc:My head is glowing.</>+10 passive Mana Regen
				req:fight1Win
				cost:100 gold
				passive:increase yield of mana by 10
			
			*manaRegen3
				name:Shiny Rock
				desc:Its REALLY shiny.</>+100 passive Mana Regen
				req:fight2Win
				cost:1000 gold
				passive:increase yield of mana by 100

Achievements
	*TEMPLATE

	// str levels
		*strMilestone1
			name:Fledgling Fighter
			desc:You reached 25 Strength!
			req:25 str

		*strMilestone2
			name:Fervent Fighter
			desc:You reached 250 Strength!
			req:250 str

		*strMilestone3
			name:Furious Fighter
			desc:You reached 2500 Strength!
			req:2500 str

	// con levels
		*constMilestone1
			name:Tavern Regular
			desc:You reached 25 Constitution!
			req:25 con

		*constMilestone2
			name:Beer Belly
			desc:You reached 250 Constitution!
			req:250 con

		*constMilestone3
			name:Goblet Goliath
			desc:You reached 2500 Constitution!
			req:2500 con

	// int levels
		*intMilestone1
			name:Smarty Pants
			desc:You reached 25 Intelligence!
			req:25 int

		*intMilestone2
			name:Puzzle Puncher
			desc:You reached 250 Intelligence!
			req:250 int

		*intMilestone3
			name:Ti-84
			desc:You reached 2500 Intelligence!
			req:2500 int

	// Focus levels
		*focusMilestone1
			name:Mindful
			desc:You reached 25 Focus!
			req:25 focus

		*focusMilestone2
			name:Enlightened
			desc:You reached 250 Focus!
			req:250 focus

		*focusMilestone3
			name:Mini Buddha
			desc:You reached 2500 Focus!
			req:2500 focus

	// Gold levels
		*goldMilestone1
			name:Clinky Coins
			desc:You reached 50 Gold!
			req:50 gold

		*goldMilestone2
			name:Money Bags
			desc:You reached 500 Gold!
			req:500 gold

		*goldMilestone3
			name:AdVenture Capitalist
			desc:You reached 5000 Gold!
			req:5000 gold

	// Fights Complete
		*TEMPLATE

			*fight1Win
				name:Novice Adventurer
				desc:You defeated the first monster!
				req:enemyAchievement=10
				on earn:
					show fight2
					toast Congratulations! You are now a Novice Adventurer!</>You have unlocked the next opponent!
				end

			*fight2Win
				name:Intermediate Adventurer
				desc:You defeated the second monster!
				req:enemyAchievement=20
				on earn:
					show fight3
					toast Congratulations! You are now an Intermediate Adventurer!</>You have unlocked the next opponent!
				end

			*fight3Win
				name:Master Adventurer
				desc:You defeated the final monster!
				req:enemyAchievement=30
				on earn:
					toast Congratulations! You are now a Master Adventurer!
				end

Shinies

Includes
	*include enemyTurn
		if (inCombat)
			lose enemyAttack health
			log The enemy attacks and you take [enemyAttack] damage!
		else
			log Error:Tried to run enemyTurn while out of combat!
		end

	*include applyAttack
		if (inCombat)
			lose str enemyHealth
			log You attack and deal [str] damage!
		else
			log Error:Tried to applyAttack while out of combat!
		end

	*include endCombat
		show fight1
		if (fight1Win) show fight2
		if (fight2Win) show fight3
		enemyAttack=0
		enemyHealth=0
		enemyGold=0
		inCombat=0
		enemyAchievement=0
		hide tag:enemyStats
		hide tag:combatActions
		hide tag:spells

	*include winBattle
		if (enemyAchievement=1)
			yield fight1Win
		else if (enemyAchievement=2)
			yield fight2Win
		else if (enemyAchievement=3)
			yield fight3Win
		end
		yield enemyGold gold
		log You have defeated the enemy!
		log You gain [enemyGold] gold!
		[include endCombat]

	*include loseBattle
		log Your health has been reduced to zero!
		log You are forced to flee!
		[include endCombat]

	*include tieBattle
		log You and your opponent strike eachother down simultaneously!
		log You are forced to flee!
		[include endCombat]

	*include checkBattleStatus
		if (enemyHealth<=0) 
			[include winBattle]
			[include endCombat]
		else if (health<=0) 
			[include loseBattle]
			[include endCombat]
		end

	*include startCombat
		if (have inCombat)
			log Error:Tried to startCombat while already in combat!
		else
			inCombat=1
			show tag:enemyStats
		end
