Let's make a game!
	name:Adventurer's Guild Clicker
	by:Nicholas Sacayan
	desc:An idle game for Professor Summerville's CS 4700.02

Settings
	// CSS reset from pastebin
	stylesheet:https://pastebin.com/raw/JxMuvhXd
	no bulk particles

CSS
	.box {
		border: thin solid white;
		overflow: hidden;
		position: static;
	}

	#box-LogBox {
		max-height:150px;
	}

	#log {
    height: 100px;
	}

	#overflow {
		overflow: auto;
	}

Layout
	*ResourceBox
		contains:Resources
		header:Resources

	*ButtonBox
		contains:Buttons
		header:Buttons

	*BuildingBox
		contains:Buildings
		header:Buildings

	*UpgradeBox
		contains:Upgrades
		header:Upgrades

	*AchievementBox
		contains:Achievements
		header:Achievements

	*CombatBox
		header:Combat Box
		contains:tag:combatStats, tag:exploreTag, tag:combatActions, tag:enemyIcon, tag:enemyStats
		ps:hide

	*LogBox
		contains:Log
		header:Log

	Buttons
		// Training options
			*TEMPLATE
				on click:
					if (no inCombat)
						yield 1 xp
					else
						log Can't increase stats while in combat!
					end
				end

			  *trainButton
			    name:Train
					desc:Increases Strength
			    on click:if (no inCombat) yield 1 str

				*drinkButton
					name:Drink
					desc:Increases Constitution
					on click:if (no inCombat) yield 1 con

				*studyButton
			    name:Study
					desc:Increases Intelligence
			    on click:if (no inCombat) yield 1 int

				*meditateButton
					name:Meditate
					desc:Increases Focus
					on click:if (no inCombat) yield 1 focus

		// Exploring
			*TEMPLATE
				on click:if (have inCombat) log You're already in combat!
				tag:exploreTag

				*exploreForestButton
					name:Explore Forest
					desc:Find monsters in the forest
					on click:
						if (inCombat=0)
							inCombat=1
							enemyHealth=10
							enemyAttack=10
							xpReward=5
							renownReward=5
							goldReward=5
							log Found an enemy!
							show rabbitIcon
							show tag:enemyStats
							hide tag:exploreTag
							show tag:combatActions
						end
					end

		// Combat
			*TEMPLATE

				*attackButton
					name:Attack
					tag:combatActions
					hidden
					on click:
						if (inCombat)
							lose str enemyHealth
							log Did [str] damage!
							lose enemyAttack health
							log Took [enemyAttack] damage!
							if (health<=0)
								log You ran out of health and fled!
								hide rabbitIcon
								hide tag:enemyStats
								show tag:exploreTag
								hide tag:combatActions
								inCombat=0
							else if (no enemyHealth)
								log You have defeated the monster!
								hide rabbitIcon
								hide tag:enemyStats
								yield goldReward gold
								yield xpReward xp
								yield renownReward renown
								inCombat=0
								hide tag:combatActions
								show tag:exploreTag
							end
						else
							log You are not in combat!
						end
					end

		// Icons
			*rabbitIcon
				tag:enemyIcon
				name:Evil Rabbit
				icon:https://imgur.com/7U428yg.jpg
				hidden

		// Memes
			//*fuelButton
				//name:Get Fuel
				//desc:Just for you Sean
				//on click:yield 1 fuel

		// Other
			*clearLogButton
				name:Clear Log
				desc:Clears the text log
				on click:clear log

			*debugButton
				name:Debug Button
				desc:Does whatever I need to debug. Remove before actual game.
				on click:
					day=360
				end

	Resources
		// Makeshift global variables
		*TEMPLATE
			hidden

		// Player combat stats
		*TEMPLATE
			start with:100
			tag:combatStats

		  *health
		    name:Health
				on tick:if (no inCombat and this<maxHealth) yield 1 this

			*maxHealth

			*mana
				name:Mana
				on tick:if (no inCombat and this<maxMana) yield 1 this

			*maxMana

		*TEMPLATE
			start with:1

			*str
				name:Strength
				desc:Increases attack damage

			*con
				name:Constitution
				desc:Increases health
				on earn:yield 1 maxHealth
				on lose:lose 1 maxHealth

			*int
				name:Intelligence
				desc:Increases the power of magic (to be added)

			*focus
				name:Focus
				desc:Increases mana
				on earn:yield 1 maxMana
				on lose:lose 1 maxMana

			*level
				name:Level
				desc:Placeholder

		*TEMPLATE

			*xp
				name:XP
				desc:Your measure of experience as an adventurer

			*gold
				name:Gold
				desc:Use this to buy things

			*renown
				name:Renown
				desc:How famous you are. Start by killing monsters.

		// Time
			*dayIncrementer
				hidden

			*day
				name:Day
				start with:1
				on tick:
					if (dayIncrementer<=8)
						yield 1 dayIncrementer
					else
						dayIncrementer=0
						yield 1 day
					end
				end

		// Hidden until obtained
			*TEMPLATE
				hidden when 0

		// Delete before Friday
			*TEMPLATE

				//*fuel
					//name:Fuel

		// Enemy Combat Stats
			*inCombat
				hidden

			*TEMPLATE
				tag:enemyStats
				hidden

				*enemyHealth
					name:Enemy Health

				*enemyAttack
					name:Enemy Attack

				*xpReward
					name:XP Reward

				*renownReward
					name:Renown Reward

				*goldReward
					name:Gold Reward

Buildings
	// Hirelings
		*TEMPLATE
			req:have renownMilestone1

			*hireling1
				name:Adventurous Commoner
				desc:A commoner who you can pay to do adventures for you.</>At base, gives +1 gold/s and +1 renown/s.(Idk how to display current values yet)
				cost:10 gold
				on tick:
					yield 1 gold
					yield 1 renown
				end

	// Renown Builders
		*TEMPLATE

			*sponserBard
				name:Sponser a Bard
				desc:Pay a bard to spread your name far and wide.</>+2 renown/s, but -1 gold/s
				cost:10 gold
				req:have renownMilestone1
				on tick:
					if (gold)
						yield 2 renown
						lose 1 gold
					end
				end

	// Gold Builders
		*lemonadeStand
			// Make a grape reference
			name:Lemonade Stand
			desc:Start a Lemonade Stand to bring in constant income</>+2 gold/s
			req:goldMilestone1
			cost:10 gold
			on tick:yield 2 gold

	// Trainers
		*strTrainer
			req:have strMilestone1
			name:Personal Trainer
			desc:Passively increases your Strength
			cost:10 gold
			on tick:yield 1 str

		*conTrainer
			req:have constMilestone1
			name:Drinking Buddy
			desc:Passively increases your Constitution
			cost:10 gold
			on tick:yield 1 con

		*intTrainer
			req:have intMilestone1
			name:Magic Tutor
			desc:Passively increases your Intelligence
			cost:10 gold
			on tick:yield 1 int

		*focusTrainer
			req:have focusMilestone1
			name:Yoga Instructor
			desc:Passively increases your Focus
			cost:10 gold
			on tick:yield 1 focus

Upgrades
	// Active Upgrades
	*trainingUpgrade1
		name:Practice Sword
		desc:+1 Strength per Train
		cost:10 gold
		passive:increase yield of trainButton by 1

	// Add kidney meme somewhere
	*drinkingUpgrade1
		name:Bigger Mug
		desc:+1 Constitution per Drink
		cost:10 gold
		passive:increase yield of drinkButton by 1

	*studyingUpgrade1
		name:Set of Bookmarks
		desc:+1 Intelligence per Study
		cost:10 gold
		passive:increase yield of studyButton by 1

	*meditateUpgrade1
		name:Scented Candles
		desc:+1 Focus per Meditate
		cost:10 gold
		passive:increase yield of meditateButton by 1

	// Building Upgrades
		*hirelingUpgrade1
			req:renownMilestone1
			cost:100 gold
			name:Adventuring Lessons
			desc:Teach your commoners to fight! x2 yield/s on commoners
			passive:multiply gain of hireling1 by 2

	// Passive Upgrades
	*regenUpgrade1
		name:First Aid Kit
		desc:+1 passive Health Regen
		cost:10 gold
		passive:increase yield of health by 1

Achievements
	// str levels
		*strMilestone1
			name:Fledgling Fighter
			desc:Others begin to acknowledge you as a fighter.
			req:25 str

	// con levels
		*constMilestone1
			name:Tavern Regular
			desc:Your name is known in the local taverns.
			req:25 con

	// int levels
		*intMilestone1
			name:Smart Commoner
			desc:The local librarians know your name.
			req:25 int

	// Focus levels
		*focusMilestone1
			name:Sodoku Solver
			desc:You feel more able to focus.
			req:25 focus

	// Renown levels
		*renownMilestone1
			name:Village hero
			desc:You are known throughout your small hamlet
			req:25 renown

	// Gold levels
		*goldMilestone1
			name:Clinky Coins
			desc:My small wallet is full
			req:50 gold

// Items
// These don't seem to work AT ALL

Shinies

Includes
