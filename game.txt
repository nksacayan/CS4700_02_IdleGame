Let's make a game!
	name:Adventurer's Guild Clicker
	by:Nicholas Sacayan
	desc:An idle game for Professor Summerville's CS 4700.02

Settings
	// CSS reset from pastebin
	stylesheet:https://pastebin.com/raw/JxMuvhXd
	no bulk particles

CSS
	.box {
		border: thin solid white;
		overflow: hidden;
		position: static;
	}

	#box-LogBox {
		max-height:150px;
	}

	#log {
    height: 100px;
	}

Layout
	*ResourceBox
		contains:Resources
		header:Resources

	*ButtonBox
		contains:Buttons
		header:Buttons

	*BuildingBox
		contains:Buildings
		header:Buildings

	*UpgradeBox
		contains:Upgrades
		header:Upgrades

	*AchievementBox
		contains:Achievements
		header:Achievements

	*CombatBox
		header:Combat Box
		contains:tag:combatStats, tag:fight, tag:combatActions, tag:spells, tag:items, tag:enemyStats
		ps:hide

	*LogBox
		contains:Log
		header:Log

	Buttons
		// Training options
			*TEMPLATE
				on click:if (inCombat) log Can't increase stats while in combat!

			  *trainButton
			    	name:Train
					desc:Increases Strength
			    	on click:if (no inCombat) yield 1 str

				*drinkButton
					name:Drink
					desc:Increases Constitution
					on click:if (no inCombat) yield 1 con

				*studyButton
			    	name:Study
					desc:Increases Intelligence
			    	on click:if (no inCombat) yield 1 int

				*meditateButton
					name:Meditate
					desc:Increases Focus
					on click:if (no inCombat) yield 1 focus

		// Fights
			*TEMPLATE
				on click:
					if (have inCombat)
						log You're already in combat!
					else
						log You've found an enemy!
						show tag:enemyStats
						show tag:combatActions
						hide tag:fight
					end
				end
				tag:fight

				*fight1
					name:First Monster
					on click:
						if (no inCombat)
							yield 10 enemyHealth
							yield 10 enemyAttack
							yield 10 enemyGold
							enemyAchievement=1
						end
						[include startCombat]
					end

				*fight2
					name:Second Monster
					hidden
					on click:
						if (no inCombat)
							yield 100 enemyHealth
							yield 100 enemyAttack
							yield 100 enemyGold
							enemyAchievement=2
						end
						[include startCombat]
					end

				*fight3
					name:Final Monster
					hidden
					on click:
						if (no inCombat)
							yield 1000 enemyHealth
							yield 1000 enemyAttack
							yield 1000 enemyGold
							enemyAchievement=3
						end
						[include startCombat]
					end

		// Combat
			*TEMPLATE
				hidden
				tag:combatActions

				*attackButton
					name:Attack
					on click:
						if (have inCombat)
							// do damage
							[include applyAttack]
							// take damage
							[include enemyTurn]
							// check battle status
							[include checkBattleStatus]
						end
					end

				*fleeButton
					name:Flee
					desc:Flee the battle!
					on click:
						[include endCombat]
						log You flee combat!
					end

				*spellButton
					name:Spells
					on click:
						show tag:spells
						hide tag:combatActions
					end

				*itemButton
					name:Items

		// Spellcasting
			*TEMPLATE
			hidden
			tag:spells

				*fireball
					name:Fireball
					desc:Lobs a fireball at your enemies for 2*intelligence ([2*int]) damage!
					on click:
						if (have inCombat)
							lose (2*int) enemyHealth
							lose (int/4) mana
							log You cast Fireball for [2*int] damage!
							[include enemyTurn]
							[include checkBattleStatus]
						end
					end

				*heal
					name:Heal
					desc:Heals self by *placeholder*
					on click:
						if (have inCombat)
							yield (2*int) health
							lose (int/4) mana
							log You cast Heal and regain [2*int] health!
							[include enemyTurn]
							[include checkBattleStatus]
						end
					end

				*spellButtonReturn
					name:Back
					desc:Hide spells and return to combat actions.
					on click:
						show tag:combatActions
						hide tag:spells
					end

		// Items
			*TEMPLATE
			hidden
			tag:items

			*itemButtonReturn
					name:Back
					desc:Hide items and return to combat actions.
					hidden

		// Other
		*TEMPLATE

			*clearLogButton
				name:Clear Log
				desc:Clears the text log
				on click:clear log

			*debugButton
				name:Debug Button
				desc:Does whatever I need to debug. Remove before actual game.
				on click:
					toast Hid ResourceBox!
					hide ResourceBox
				end

	Resources
		// Makeshift global variables
		*TEMPLATE
			hidden

			*enemyAchievement

		// Player combat stats
		*TEMPLATE
			start with:100
			tag:combatStats

			*health
		    	name:Health
					on tick:if (no inCombat and this<maxHealth) yield 1 this

			*maxHealth
				name:Max Health

			*mana
				name:Mana
				on tick:if (no inCombat and this<maxMana) yield 1 this

			*maxMana
				name:Max Mana

		*TEMPLATE
			start with:1

			*str
				name:Strength
				desc:Increases attack damage

			*con
				name:Constitution
				desc:Increases health
				on earn:yield 1 maxHealth
				on lose:lose 1 maxHealth

			*int
				name:Intelligence
				desc:Increases the power of magic (to be added)

			*focus
				name:Focus
				desc:Increases mana
				on earn:yield 1 maxMana
				on lose:lose 1 maxMana

			*level
				name:Level
				desc:Placeholder

		*TEMPLATE

			*gold
				name:Gold
				desc:Use this to buy things

		// Enemy Combat Stats
			*inCombat
				hidden

			*TEMPLATE
				tag:enemyStats
				hidden

				*enemyHealth
					name:Enemy Health

				*enemyAttack
					name:Enemy Attack

				*enemyGold
					name:Gold Reward
					desc:How much gold you get for killing this enemy.

Buildings

	// Gold Builders
		*lemonadeStand
			// Make a grape reference
			name:Lemonade Stand
			desc:Start a Lemonade Stand to bring in constant income</>+2 gold/s
			req:goldMilestone1
			cost:10 gold
			on tick:yield 2 gold

	// Trainers
		*strTrainer
			req:have strMilestone1
			name:Personal Trainer
			desc:Passively increases your Strength
			cost:10 gold
			on tick:yield 1 str

		*conTrainer
			req:have constMilestone1
			name:Drinking Buddy
			desc:Passively increases your Constitution
			cost:10 gold
			on tick:yield 1 con
			// might not increase max health

		*intTrainer
			req:have intMilestone1
			name:Magic Tutor
			desc:Passively increases your Intelligence
			cost:10 gold
			on tick:yield 1 int

		*focusTrainer
			req:have focusMilestone1
			name:Yoga Instructor
			desc:Passively increases your Focus
			cost:10 gold
			on tick:yield 1 focus

Upgrades
	// Active Upgrades
	*trainingUpgrade1
		name:Practice Sword
		desc:+1 Strength per Train
		cost:10 gold
		passive:increase yield of trainButton by 1

	// Add kidney meme somewhere
	*drinkingUpgrade1
		name:Bigger Mug
		desc:+1 Constitution per Drink
		cost:10 gold
		passive:increase yield of drinkButton by 1

	*studyingUpgrade1
		name:Set of Bookmarks
		desc:+1 Intelligence per Study
		cost:10 gold
		passive:increase yield of studyButton by 1

	*meditateUpgrade1
		name:Scented Candles
		desc:+1 Focus per Meditate
		cost:10 gold
		passive:increase yield of meditateButton by 1

	// Building Upgrades

	// Passive Upgrades
	*regenUpgrade1
		name:First Aid Kit
		desc:+1 passive Health Regen
		cost:10 gold
		passive:increase yield of health by 1

Achievements
	*TEMPLATE

	// str levels
		*strMilestone1
			name:Fledgling Fighter
			desc:Others begin to acknowledge you as a fighter.
			req:25 str

	// con levels
		*constMilestone1
			name:Tavern Regular
			desc:Your name is known in the local taverns.
			req:25 con

	// int levels
		*intMilestone1
			name:Smart Commoner
			desc:The local librarians know your name.
			req:25 int

	// Focus levels
		*focusMilestone1
			name:Sudoku Solver
			desc:You feel more able to focus.
			req:25 focus

	// Gold levels
		*goldMilestone1
			name:Clinky Coins
			desc:My small wallet is full
			req:50 gold

	// Fights Complete
		*TEMPLATE

			*fight1Win
				name:Novice Adventurer
				req:enemyAchievement=10
				on earn:show fight2

			*fight2Win
				name:Intermediate Adventurer
				req:enemyAchievement=20
				on earn:show fight3

			*fight3Win
				name:Master Adventurer
				req:enemyAchievement=30

Shinies

Includes
	*include enemyTurn
		if (inCombat)
			lose enemyAttack health
			log The enemy attacks and you take [enemyAttack] damage!
		else
			log Error:Tried to run enemyTurn while out of combat!
		end

	*include applyAttack
		if (inCombat)
			lose str enemyHealth
			log You attack and deal [str] damage!
		else
			log Error:Tried to applyAttack while out of combat!
		end

	*include endCombat
		enemyAttack=0
		enemyHealth=0
		enemyGold=0
		inCombat=0
		enemyAchievement=0
		hide tag:enemyStats
		hide tag:combatActions
		show fight1
		if (fight1Win) show fight2
		if (fight2Win) show fight3

	*include winBattle
		if (enemyAchievement=1)
			yield fight1Win
		else if (enemyAchievement=2)
			yield fight2Win
		else if (enemyAchievement=3)
			yield fight3Win
		end
		yield enemyGold gold
		log You have defeated the enemy!
		log You gain [enemyGold] gold!
		[include endCombat]

	*include loseBattle
		log Your health has been reduced to zero!
		log You are forced to flee!
		[include endCombat]

	*include testInclude %stringParameter="Empty stringParameter" %intParameter="100"
		log You hit the test include!
		log The stringParameter is [%stringParameter]
		log Going to increase days by [%intParameter]
		yield [%intParameter] day

	*include checkBattleStatus
		if (have inCombat)
			if (enemyHealth<=0)
				[include winBattle]
			else if (health<=0)
				[include loseBattle]
			end
		else
			log Error:Tried to checkBattleStatus while not in combat!
		end

	*include startCombat
		if (have inCombat)
			log Error:Tried to startCombat while already in combat!
		else
			inCombat=1
			show tag:enemyStats
		end
